/*
 Copyright (c) 2014,2015 Ahome' Innovation Technologies. All rights reserved.

 Licensed under the Apache License, Version 2.0 (the "License");
 you may not use this file except in compliance with the License.
 You may obtain a copy of the License at

     http://www.apache.org/licenses/LICENSE-2.0

 Unless required by applicable law or agreed to in writing, software
 distributed under the License is distributed on an "AS IS" BASIS,
 WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 See the License for the specific language governing permissions and
 limitations under the License.
*/
import java.util.List

apply plugin: 'groovy'
apply plugin: 'maven'

sourceCompatibility = 1.7

version = '1.0.1-SNAPSHOT'

group = 'com.ahome-it'

jar {
    manifest {
        attributes 'Implementation-Title': 'Ahome-Tooling-Server-Core', 'Implementation-Version': version
    }
    from sourceSets.main.allSource
}

repositories {
    mavenCentral()
    maven { url "${sonatype_snapshotURL}" }
}

sourceSets {
  main {
    resources {
      source groovy
    }
  }
}

tasks.withType(GroovyCompile) {
    groovyOptions.optimizationOptions.indy = true
}

configurations {
    compile.exclude(group: 'org.slf4j', module: 'log4j-over-slf4j')
}

dependencies {
    springCorePackages().each { String name ->
        compile(group: 'org.springframework', name: name, version: '4.1.6.RELEASE')
    }
    springSecurityPackages().each { String name ->
        compile(group: 'org.springframework.security', name: name, version: '4.0.0.RELEASE')
    }
    compile(group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.3.11', classifier: 'indy')
    compile(group: 'log4j', name: 'log4j', version: '1.2.17')
    compile(group: 'com.google.guava', name: 'guava', version: '18.0')
    compile(group: 'commons-io', name: 'commons-io', version: '2.4')
    compile(group: 'commons-beanutils', name: 'commons-beanutils', version: '1.9.2')
    compile(group: 'commons-fileupload', name: 'commons-fileupload', version:'1.3.1')
    compile(group: 'com.ahome-it', name: 'ahome-tooling-json', version: '1.0.6-SNAPSHOT')
    compile(group: 'javax.mail', name: 'mail', version: '1.4.7')
    compile(group: 'javax.activation', name: 'activation', version: '1.1.1')
    compile(group: 'javax.websocket', name:'javax.websocket-api', version:'1.1')
    compile(group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0')
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "${sonatype_snapshotURL}") {
                authentication(userName: "${sonatype_user}", password: "${sonatype_password}")
            }
        }
    }
}

def List<String> springCorePackages() {
    return [
        'spring-core',
        'spring-beans',
        'spring-context',
        'spring-context-support',
        'spring-expression',
        'spring-aop',
        'spring-aspects',
        'spring-instrument',
        'spring-instrument-tomcat',
        'spring-messaging',
        'spring-jdbc',
        'spring-tx',
        'spring-orm',
        'spring-oxm',
        'spring-jms',
        'spring-web',
        'spring-webmvc',
        'spring-websocket',
        'spring-webmvc-portlet',
        'spring-test'
    ]
}

def List<String> springSecurityPackages() {
    return [
        'spring-security-web',
        'spring-security-acl',
        'spring-security-cas',
        'spring-security-ldap',
        'spring-security-openid',
        'spring-security-crypto',
        'spring-security-config',
        'spring-security-aspects',
        'spring-security-remoting'
    ]
}